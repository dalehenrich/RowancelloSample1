#!/usr/bin/env superdoit_stone
options
{
	SuperDoitRequiredOptionWithRequiredArg long: 'sourceFile' .
	SuperDoitRequiredOptionWithRequiredArg long: 'resultsFile' .
	SuperDoitOptionalOptionWithNoArg long: 'inspect' .
	SuperDoitOptionalOptionWithNoArg long: 'validate' .
}
%
#
#options
#{
#  SuperDoitOptionalOptionWithNoArg long: 'help' short: 'h'.
#  SuperDoitOptionalOptionWithNoArg long: 'debug' short: 'D'.
#}
#%
# Example options section
#
#options
#{
#	SuperDoitOptionalOptionWithNoArg long: 'noarg'.
#	SuperDoitOptionalOptionWithNoArg long: 'noarg' short: 'n'.
#
#	SuperDoitOptionalOptionWithRequiredArg long: 'optional'.
#	SuperDoitOptionalOptionWithRequiredArg long: 'optional' default: 'default'.
#	SuperDoitOptionalOptionWithRequiredArg long: 'optional' short: 'o'.
#	SuperDoitOptionalOptionWithRequiredArg long: 'optional' short: 'o' default: 'default'.
#
#	SuperDoitRequiredOptionWithRequiredArg long: 'required'.
#	SuperDoitRequiredOptionWithRequiredArg long: 'required' short: 'r'.
#}
#%
#
usage
-----
USAGE 
  # with GS_HOME set (<stone-name> optional if run in $GS_HOME/servers/stones/<stone-name> directory)
  $basename [--help | -h] [--debug | -D]  [--debugGem] [-- [<stone-name> [<topaz-command-line-args>] ] ]
  # with GEMSTONE set
  $basename [--help | -h] [--debug | -D]  [--debugGem] -- (-r | -l | -L) -I <path-to-.topazini> [<topaz-command-line-args>]

DESCRIPTION
  Take a generated cluster structure (rowanV3_Rowan_components.ston for example) produce a new
  cluster structure that represents the INTENT of the original component structure.

	Overall goal is to be able to create a 3.7.5 RowanV3.5 image that supports only v3 components and methods,
	excluding the v2 api, classes and tests.

	Then create a 3.7.5 RowanV4.0 image that onlys supports clusters and subclusters ... with v2 and v3 support optionally loadable.
	it is likely that this will require repackaging and fine-tuning the meaning of v2 api.

	New cluster name/old subclusters (must account for all subclusters):
		AST:
			common_platforms_gemstone_AST
			common_tests_platforms_gemstone_Tests
		Stubs:
			common_stubs_gemstone37-_Core
		Obsolete and targetted to not be in standard V4 image:
			common_obsolete_gemstone_Obsolete
		Obsolete and not loaded into a standard V3 image (kept around for historical reasons):
			common_obsolete_ObsoleteReaderWriter
			common_tests_obsolete_Tests
		V2:
			common_v2_Specifications
			common_v2_Components
			common_v2_Core
			common_v2_v2Only_platforms_gemstone_Definitions
			common_tests_v2_v2Only_Components
			common_v2_platforms_gemstone_Core
			common_v2_platforms_gemstone_Loader
			common_v2_Definitions
			common_tests_v2_Components
			common_v2_platforms_gemstone_Definitions
			common_v2_v2Only_platforms_gemstone_Loader
			common_v2_platforms_gemstone36-_Loader
			common_v2_v2Only_platforms_gemstone36-_Loader
			common_tests_v2_Specifications
			common_v2_platforms_gemstoneBase_Components
			common_tests_v2_Tests
			common_v2_Tools
			common_v2_v2Only_Tools
		V4 (V3 in V3.5 image):
			common_v3_Core
			common_tests_v3_Tests
		tonel:
			common_tonel_Core
		Rowan-Core:
			(unconditional)
			common_Components
			common_Core
			common_Definitions
			common_Specifications
			common_Tools
			(conditional)
			common_platforms_gemstone_Tools
	"

OPTIONS
  <stone-name>               Name of the GsDevKit_home stone. <stone-name> argument
                             may be skipped if the script is run in a GsDevKit_home
                             stone directory (i.e., $GS_HOME/server/stones/<stone-name>
  <topaz-command-line-args>  topaz options that should be passed to topaz when running
                             running the script
  -h, --help                 display usage message
  -D, --debug                bring up topaz debugger in the event of a script error
  --debugGem                 If terminal is connected to stdout, bring up debugger. If not,
                             dump stack to stdout and wait for topaz to attach using topaz
                             DEBUGGEM command.

EXAMPLES
  $basename --help                           -- gs_351             # with GS_HOME set
  $basename -h                               -- -l -I ./.topazini  # with GEMSTONE set
  $basename -D <script-arguments>            <topaz-arguments>
  $basename --debugGem <script-arguments>    <topaz-arguments>
  $basename <script-arguments>               <topaz-arguments>

  $basename  --sourceFile=/bosch1/users/dhenrich/_stones/git/RowancelloSample1/generated/rowanV3_Rowan_components.ston \
             --resultsFile=/bosch1/users/dhenrich/_stones/git/RowancelloSample1/generated/rowanV3_Rowan_clusters.ston \
             -D
  $basename  --sourceFile=/bosch1/users/dhenrich/_stones/git/RowancelloSample1/generated/rowanV3_Rowan_components.ston \
             --resultsFile=/bosch1/users/dhenrich/_stones/git/RowancelloSample1/generated/rowanV3_Rowan_clusters.ston \
             --inspect
             -D
  $basename  --sourceFile=/bosch1/users/dhenrich/_stones/git/RowancelloSample1/generated/rowanV3_Rowan_components.ston \
             --resultsFile=/bosch1/users/dhenrich/_stones/git/RowancelloSample1/generated/rowanV3_Rowan_clusters.ston \
             --validate
             -D
-----
%
instvars
proposedProjectClusters
projectClusters
rowanV40Slice
rowanV40_loadedSlice
rowanV35Slice
%
method
defineSlices
	"Slices are the top-level, loadable objects. A slice is defined as a list of clusters. This method will define the slices
		and as clusters are created they will be added to the appropriate slice."

	proposedProjectClusters
		addSliceNamed: 'RowanV3.5' 
			comment: 'The RowanV3.5 slice is intended to duplicated the packages loaded into the standard 3.7.5 Rowan3 extent';
		addSliceNamed: 'RowanV4.0' 
			comment: 'The RowanV4.0 slice is intended to include only RowanV4.0 functionality. ';
		addSliceNamed: 'RowanV4.0_loaded' 
			comment: 'The RowanV4.0_loaded slice is intended to include RowanV4.0 plus conditional v3, v2 and v2Only to allow RowanV4.0 to read v3 and v2 format repositories';
		yourself	
%
method
refactorStubs
	"The methods managed in the Stubs packages are methods that are the interface between common code for Rowan and the Base.
		The methods are implemented in Behavior, Class, and GsNMethod and are intended to allow code to use the same set of methods
		wheteher or not the code is run in the Base or Rowan. With the use of stub methods, code browsers and tools like topaz do not
		need to have a special case when run in Rowan or the Base."
	| common_stubs_gemstone37_Core stubsCluster stubsSubcluster |
	common_stubs_gemstone37_Core :=  projectClusters subclusterNamed: 'common_stubs_gemstone37-_Core'.
	stubsCluster :=  proposedProjectClusters 
		addClusterNamed: 'Stubs' 
		comment: 'The methods managed in the GemStone-Stubs cluster are methods that are the interface methods between common code for Rowan and the Base.  The methods are implemented in Behavior, Class, and GsNMethod and are intended to allow code to use methods a common set of method names wheteher or not the code is run in the Base or Rowan. With the use of stub methods, code browsers and tools like topaz do not need to have a special case when run in Rowan or the Base. Note that the methods managed by the Obsolete-GemStone cluster will no longer be needed in the image'.
	rowanV40Slice addClusterNamed: stubsCluster clusterName.
	rowanV40_loadedSlice addClusterNamed: stubsCluster clusterName.
	rowanV35Slice addClusterNamed: stubsCluster clusterName.
	stubsSubcluster := proposedProjectClusters 
		addSubclusterNamed: 'GemStone-Stubs' 
		toClusterNamed: 'Stubs' 
		comment: 'GemStone Behavior, Class and GsNMethod glue methods'.
	stubsSubcluster
		addPackagesNamed: common_stubs_gemstone37_Core packageNames;
		condition: 'gs3.[7-]'.
%
method
refactorAST	
	"common_platforms_gemstone_AST"
	"AST project was ported from GLASS implementation"
	| common_platforms_gemstone_AST common_tests_platforms_gemstone_Tests astCluster astSubcluster astTestSubcluster |
	common_platforms_gemstone_AST := projectClusters subclusterNamed: 'common_platforms_gemstone_AST'.
	common_tests_platforms_gemstone_Tests := projectClusters subclusterNamed: 'common_tests_platforms_gemstone_Tests'.

	astCluster := proposedProjectClusters addClusterNamed: 'AST' comment: 'Provide method format support for GemStone'.
	rowanV40Slice addClusterNamed: astCluster clusterName.
	rowanV40_loadedSlice addClusterNamed: astCluster clusterName.
	rowanV35Slice addClusterNamed: astCluster clusterName.

	astSubcluster := proposedProjectClusters addSubclusterNamed: 'AST-Core' toClusterNamed: 'AST' comment: 'AST core'.
	astSubcluster 
		addPackagesNamed: common_platforms_gemstone_AST packageNames;
		condition: 'gemstone'.

	astTestSubcluster := proposedProjectClusters addSubclusterNamed: 'AST-Core-Tests' toClusterNamed: 'AST' comment: 'AST tests'.
	astTestSubcluster
		addPackagesNamed: ((common_tests_platforms_gemstone_Tests packageNames) select: [:each | each beginsWith: 'AST']);
		condition: 'tests & gemstone'.

	proposedProjectClusters
		expressionContext
			set: 'tests' to: true;
			yourself
%
method
refactorObsolete
	"
		The Rowan-Obsolete-Core and Rowan-Obsolete-Tests (obsoleteReaderWriter) packages are not loaded in a 
		standard Rowan:masterV3.5 image and are kept around for historical reasons.

			'common_obsolete_ObsoleteReaderWriter' : RwSubclusterV2 {
				#subclusterName : 'common_obsolete_ObsoleteReaderWriter',
				#packageNames : [
					'Rowan-Obsolete-Core'
				],
				#comment : 'subcluster generated from common/obsolete/ObsoleteReaderWriter',
				#condition : 'obsoleteReaderWriter'
			},
			'common_tests_obsolete_Tests' : RwSubclusterV2 {
				#subclusterName : 'common_tests_obsolete_Tests',
				#packageNames : [
					'Rowan-Obsolete-Tests'
				],
				#comment : 'subcluster generated from common/tests/obsolete/Tests',
				#condition : 'tests & obsoleteReaderWriter'
			},

		The obsolete methods and classes are classes and methods that are 'scheduled' to be removed from Rowan.

			'common_obsolete_gemstone_Obsolete' : RwSubclusterV2 {
				#subclusterName : 'common_obsolete_gemstone_Obsolete',
				#packageNames : [
					'Rowan-Obsolete-GemStone'
				],
				#comment : 'ObsoleteTools gemstone  packages ',
				#condition : 'obsolete & gemstone'
			},
	"
	| common_obsolete_gemstone_Obsolete obsoleteCluster obsoleteSubcluster |
	common_obsolete_gemstone_Obsolete :=  projectClusters subclusterNamed: 'common_obsolete_gemstone_Obsolete'.
	obsoleteCluster :=  proposedProjectClusters 
		addClusterNamed: 'Obsolete-GemStone' 
		comment: 'The methods in the Obsolete-GemStone cluster are scheduled to be removed from the system for Rowan V4 in favor of the standard API methods, i.e., those methods that have been stubbed out (see the Stubs cluster). Currently they are present in a standard V3 image. They will be removed when the Stubs work is complete'.
	rowanV35Slice addClusterNamed: obsoleteCluster clusterName.
	obsoleteSubcluster := proposedProjectClusters 
		addSubclusterNamed: 'Obsolete-GemStone' 
		toClusterNamed: 'Obsolete-GemStone' 
		comment: 'GemStone Behavior, Class and GsNMethod glue methods'.
	obsoleteSubcluster
		addPackagesNamed: common_obsolete_gemstone_Obsolete packageNames;
		condition: 'gemstone'.
%
method
refactorV2
	"Create a cluster that collects all of the v2 subclusters"
	| v2Cluster reverseConditionMap |
	v2Cluster := proposedProjectClusters
		addClusterNamed: 'V2'
		comment: 'Collection of subclusters that are conditional on v2 condition. Referencing this cluster with v2 condition set will load v2 support into image'.
	reverseConditionMap := Dictionary new.
	rowanV35Slice addClusterNamed: v2Cluster clusterName.
	rowanV40_loadedSlice  addClusterNamed: v2Cluster clusterName.
	{
		'common_v2_Specifications'.
		'common_v2_Components'.
		'common_v2_Core'.
		'common_v2_v2Only_platforms_gemstone_Definitions'.
		'common_tests_v2_v2Only_Components'.
		'common_v2_platforms_gemstone_Core'.
		'common_v2_platforms_gemstone_Loader'.
		'common_v2_Definitions'.
		'common_tests_v2_Components'.
		'common_v2_platforms_gemstone_Definitions'.
		'common_v2_v2Only_platforms_gemstone_Loader'.
		'common_v2_platforms_gemstone36-_Loader'.
		'common_v2_v2Only_platforms_gemstone36-_Loader'.
		'common_tests_v2_Specifications'.
		'common_v2_platforms_gemstoneBase_Components'.
		'common_tests_v2_Tests'.
		'common_v2_Tools'.
		'common_v2_v2Only_Tools'.
	} do: [:subclusterName |
		| subcluster originalSubcluster newExpression |
		originalSubcluster := projectClusters subclusterNamed: subclusterName.
		newExpression := self stripUnnecessaryVariable: 'v2Only' from: originalSubcluster condition.
		newExpression := self stripUnnecessaryVariable: 'v2' from: newExpression.
		newExpression = 'common'
			ifTrue: [ 
				"add the packages to the current cluster"
				v2Cluster addPackagesNamed: originalSubcluster packageNames ]
			ifFalse: [ 
				"create a new subcluster for packages or add packages to existing subcluster with same conditional expression"
				(reverseConditionMap at: newExpression ifAbsent: [])
					ifNil: [ 
						"create a subcluster to record packages for newExpression"
						subcluster := proposedProjectClusters 
							addSubclusterNamed: (subclusterName copyFrom: 'common_' size +1 to: subclusterName size) 
							toClusterNamed: 'V2'
							comment: originalSubcluster comment.
						subcluster 
							addPackagesNamed: originalSubcluster packageNames;
							condition: newExpression.
						reverseConditionMap at: originalSubcluster condition put: subcluster ]
					ifNotNil: [:existingSubcluster |
						"add the packages to the existingSubcluster with matching conditional expression"
						existingSubcluster
							addPackagesNamed: originalSubcluster packageNames;
							comment: existingSubcluster comment, ' [', originalSubcluster subclusterName, '] ', originalSubcluster comment ] ] ].
	proposedProjectClusters
		expressionContext
			set: 'v4' to: true;
			set: 'tests' to: true;
			yourself
%
method
stripUnnecessaryVariable: variable from: expression 
	| newExpression |
	expression = variable 
		ifTrue: [
			"unconditionally add the packages to the parent cluster"
			^ 'common' ].
	newExpression := expression copyReplaceAll: '& ', variable, ' &' with: '&'.
	newExpression := newExpression copyReplaceAll: variable, ' &' with: ''.
	newExpression := newExpression copyReplaceAll: '& ', variable with: ''.
	^ newExpression trimBoth
%
method
refactorV4
	"Create a cluster that collects all of the v3 subclusters ... which are actually V4 functionality"
	| common_v3_Core common_tests_v3_Tests v4Cluster v4Subcluster v4TestsSubcluster |
	common_v3_Core := projectClusters subclusterNamed: 'common_v3_Core'.
	common_tests_v3_Tests := projectClusters subclusterNamed: 'common_tests_v3_Tests'.
	v4Cluster :=  proposedProjectClusters 
		addClusterNamed: 'V4' 
		comment: 'The methods in V4 cluster are currently used in the standard V3 image to implement early V4 support.'.
	rowanV40Slice addClusterNamed: v4Cluster clusterName.
	rowanV40_loadedSlice addClusterNamed: v4Cluster clusterName.

	v4Subcluster := proposedProjectClusters addSubclusterNamed: 'V4-Shared' toClusterNamed: 'V4' comment: 'V4 api support shared with Rowan V3'.
	v4Subcluster 
		addPackagesNamed: common_v3_Core packageNames;
		condition: 'v4'.
	v4TestsSubcluster := proposedProjectClusters addSubclusterNamed: 'V4-Shared-Tests' toClusterNamed: 'V4' comment: 'V4 api tests shared with Rowan V3'.
	v4TestsSubcluster 
		addPackagesNamed: common_tests_v3_Tests packageNames;
		condition: 'v4 & tests'.
%
method
refactorTonel
	"Create a cluster for tonel support in gemstone"
	| common_tonel_Core tonelCluster tonelSubcluster |
	common_tonel_Core := projectClusters subclusterNamed: 'common_tonel_Core'.
	tonelCluster :=  proposedProjectClusters 
		addClusterNamed: 'Tonel' 
		comment: 'The methods in Tonel cluster implement tonel package format.'.
	rowanV40Slice addClusterNamed: tonelCluster clusterName.
	rowanV40_loadedSlice addClusterNamed: tonelCluster clusterName.

	tonelSubcluster := proposedProjectClusters addSubclusterNamed: 'Tonel' toClusterNamed: 'Tonel' comment: 'Tonel support'.
	tonelSubcluster 
		addPackagesNamed: common_tonel_Core packageNames;
		condition: 'gemstone'.
%
method
refactorRowanCommon
	"Create a cluster for the common Rowan functionality ... parts of this guy might be distrbuted to V3/V4 as neeed"
	| rowanCluster |
	rowanCluster := proposedProjectClusters
		addClusterNamed: 'Rowan-Core'
		comment: 'Collection of packages and subclusters defining the Rowan core functionality'.
	{
			'common_Components'.
			'common_Core'.
			'common_Definitions'.
			'common_Specifications'.
			'common_Tools'.
	} do: [:subclusterName |
		| originalSubcluster |
		originalSubcluster := projectClusters subclusterNamed: subclusterName.
		rowanCluster addPackagesNamed: originalSubcluster packageNames ].
	rowanV35Slice addClusterNamed: rowanCluster clusterName.
	rowanV40Slice  addClusterNamed: rowanCluster clusterName.
	rowanV40_loadedSlice  addClusterNamed: rowanCluster clusterName.

	"Collection of subclusters (conditional) to be added to Rowan-Core cluster"
	{
		'common_platforms_gemstone_Tools' .
	} do:  [:subclusterName |
		| subcluster originalSubcluster |
		originalSubcluster := projectClusters subclusterNamed: subclusterName.
		subcluster := proposedProjectClusters 
			addSubclusterNamed: (subclusterName copyFrom: 'common_' size +1 to: subclusterName size) 
			toClusterNamed: rowanCluster clusterName
			comment: originalSubcluster comment.
		subcluster 
			addPackagesNamed: originalSubcluster packageNames;
			condition: originalSubcluster condition  ].
%
method
validateRowanCoreSubclusters
	|calculatedSubclusterPackageNames expectedPackageNames |
	calculatedSubclusterPackageNames := Set new.
	{
		"Rowan-Core:"
			'common_Components' .
			'common_Core' .
			'common_Definitions' .
			'common_Specifications' .
			'common_Tools' .
	} do: [ :subclusterName | 
		calculatedSubclusterPackageNames addAll: (projectClusters subclusterNamed: subclusterName) packageNames ].
	expectedPackageNames := (proposedProjectClusters clusterNamed: 'Rowan-Core') packageNames asSet.
	calculatedSubclusterPackageNames = expectedPackageNames ifFalse: [ self error: 'mismatch between calculated Rowan-Core package names and expected' ].
%
method
todo
	"common_tests_platforms_gemstone_Tests has two more packages not processed: Rowan-DataCurator-Tests, Rowan-Tests-GemStone"
%
doit
	" transform the generated projectCluster structure into the desired Rowan cluster structure 
		... and evaluate the implementation of the desired structure for expressing the real intent
		and then evaluate suitability for forming the basis for Jadeite tools"

	"at this point in time, the clusters ARE NOT INTENDED to be the top-level loadable objects ... I expect the top-level
   loadable objects will be called RwSlices ... which I have not yet created ..."

	| clusterStructure |
	proposedProjectClusters := RwResolvedProjectClusters new.
	self sourceFile asFileReference readStreamDo: [:stream |
		clusterStructure := STON fromStream: stream ].
	projectClusters := (clusterStructure at: 6) value.
	self defineSlices.
	rowanV40Slice := proposedProjectClusters sliceNamed: 'RowanV4.0'.
	rowanV40_loadedSlice := proposedProjectClusters sliceNamed: 'RowanV4.0_loaded'.
	rowanV35Slice := proposedProjectClusters sliceNamed: 'RowanV3.5'.
	self
		refactorAST;
		refactorStubs;
		refactorObsolete;
		refactorV2;
		refactorV4;
		refactorTonel;
		refactorRowanCommon;
		yourself.
	self resultsFile asFileReference writeStreamDo: [:stream |
		stream truncate.
		STON 
			put: proposedProjectClusters
			onStreamPretty: stream ].
	self inspect ifTrue: [ proposedProjectClusters halt ].
	self validate ifTrue: [ self validateRowanCoreSubclusters ].
	^ true
%
